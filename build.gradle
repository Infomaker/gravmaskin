buildscript {
    ext.kotlin_version = "1.2.60"
    ext.tornadofx_version = "1.7.17"
    ext.junit_version = "5.4.1"

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.1.0"
    }
}

apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "org.junit.platform.gradle.plugin"

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "no.tornado:tornadofx:$tornadofx_version"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    compile 'org.apache.poi:poi-ooxml:3.17'
    compile 'io.reactivex.rxjava2:rxjava:2.2.7'
    compile  'com.jakewharton.rxrelay2:rxrelay:2.1.0'

    compile  'com.squareup.okhttp3:okhttp:3.13.1'
    compile  'com.squareup.okhttp3:logging-interceptor:3.13.1'

    compile 'com.google.code.gson:gson:2.8.5'
    compile 'com.squareup.retrofit2:retrofit:2.5.0'
    compile 'com.squareup.retrofit2:converter-gson:2.5.0'
    compile 'io.reactivex.rxjava2:rxjavafx:2.1.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    compile 'no.tornado:tornadofx-controlsfx:0.1'
    compile 'de.jensd:fontawesomefx:8.9'
}

mainClassName = "se.infomaker.actapublica.app.MyApp"

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(" "),
                "Main-Class": mainClassName
        )
    }
    from(configurations.compile.collect { entry -> zipTree(entry) }) {
        exclude "META-INF/MANIFEST.MF"
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

task copyMacOSFiles (type: Copy) {
    from 'src/main/app-resources'
    into 'build/app/'
}

task copyMacOSJarFiles (type: Copy) {
    from 'build/libs/actapublica-hack2019.jar'
    from configurations.runtime
    into 'build/app/Gravmaskin.app/Contents/Resources/Java/repo/se/infomaker/actapublica'
}

build.dependsOn(copyMacOSFiles)
build.dependsOn(copyMacOSJarFiles)